import org.apache.tools.ant.filters.ReplaceTokens

import java.nio.file.Files

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply from: "${project.rootDir}/gradle/eclipse.gradle"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.jk1:gradle-license-report:0.3.12"
  }
}
apply plugin: "com.github.jk1.dependency-license-report"

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'de.mediathekview'
version = '1.0.1'

def jarName = 'MServerGUI.jar'
def mainClass = 'de.mediathekview.mserver.ui.gui.MServerGUI'

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}
compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}

task copyRuntimeLibs(type: Copy) {
    into "libs"
    from configurations.testRuntime - configurations.runtime
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

configurations {
    deployerJars
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://packagecloud.io/mediathekview/MediathekView/maven2"
    }
}

dependencies {
    compile 'de.mediathekview:MServer:4.0.0-SNAPSHOT'
    compile 'org.controlsfx:controlsfx:8.40.13'
    compile 'org.kordamp.ikonli:ikonli-core:1.9.0'
    compile 'com.google.guava:guava:23.0'
    
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-junit:4.0.+"
    testCompile "junit:junit:4.12"
    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
    
}

configurations.compile {
    resolutionStrategy.dependencySubstitution {
        def substituteIfExists = { String moduleName, String projectName ->
            if(findProject(projectName)) {
               assert !(project.hasProperty('org.gradle.configureondemand') && Boolean.valueOf(project.getProperty('org.gradle.configureondemand'))) : "org.gradle.configureondemand = true will cause problems when substituting a project"
               logger.lifecycle "$project: Substituting module '$moduleName' with project $projectName"
               substitute module(moduleName) with project(projectName)
            }
        }
        substituteIfExists('de.mediathekview:MLib', ':MLib')
    }
}

ext {
    propsFile = file('src/main/resources/version.properties').absoluteFile
    if (!propsFile.exists()) {
        Files.createFile(propsFile.toPath())
    }
}

def loadVersionProperties() {
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())
    return props
}

task updateVersion {
    doLast {
        Properties props = loadVersionProperties()
        def oldVersion = props.getProperty('version')
        if (!oldVersion.equals(project.version)) {
            logger.lifecycle "==MServer GUI======================"
            logger.lifecycle "Version: $project.version"
            logger.lifecycle "==mserver======================"
            props.setProperty('version', project.version)
            props.store(propsFile.newWriter(), null)
        }
    }
}

processResources.dependsOn updateVersion

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        uniqueVersion=false
        def repoPw;
        
        if(project.hasProperty('repoPw'))
        {
           repoPw = project.property('repoPw');
        }else
        {
            repoPw = System.getenv('PACKAGECLOUD_TOKEN');
        }
        
        repository(url: "packagecloud+https://packagecloud.io/mediathekview/MediathekView/") {
            authentication(password:  repoPw)
        }
    }
}
build.dependsOn(install)

licenseReport {
    outputDir = "build/Copyright/"
    excludes = [':MServerGUI', 'MediathekView:MLib', 'MediathekView:MServer']
    configurations = ['compile']
}

processResources.dependsOn generateLicenseReport

[distZip, distTar]*.shouldRunAfter compileJava, jar
distTar.compression = Compression.GZIP
distTar.extension = 'tar.gz'

jar {
    manifest {
        attributes(
                'Main-Class': mainClass,
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
        )
    }
    archiveName = jarName
}

distributions {
    main {
        baseName = 'MServerGUI'
        contents {
            into('lib') {
                from configurations.compile
            }
            from('scripte/runScripts') {
                filesMatching('**') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
            }
            from('build/libs') {
                include '*.jar'
            }
            from('build') {
                include 'Copyright/*'
            }
        }
    }
}

/**
 * This is how you pass arguments: "./gradlew run -Pargs=arg1,arg2,arg3
 */
task run(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }
}

/**
 * HOWTO debug:
 *  1. run "gradle debug"
 *  2. Call your IDE to connect to a remote java application on port 5005.
 *
 * This is how you pass arguments: "./gradlew debug -Pargs=arg1,arg2,arg3
 */
task debug(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
    debug true

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if a new releass (tag) will be build!<b/>
 * Call this so: './gradlew build release -PrepoPw=[PASSWORD]'
 */
task release(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Version '+version
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if you push to develop!<b/>
 * Call this so: './gradlew build releaseSnapshot -PrepoPw=[PASSWORD]'
 */
task releaseSnapshot(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Snapshot Version '+version
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version = version+'-SNAPSHOT'
    }
}

